plugins {
    id "com.jfrog.bintray" version "1.8.4"
    id 'net.researchgate.release' version '2.6.0'
}

apply {
    plugin "base"
    plugin "java"
    plugin "checkstyle"
    plugin "jacoco"
    plugin "idea"
    plugin "eclipse"
    plugin 'maven'
    plugin 'maven-publish'
}

task wrapper(type: Wrapper) {
  gradleVersion = '4.6'
}

repositories {
    mavenLocal()
    mavenCentral()
}

sourceCompatibility = JavaVersion.VERSION_1_8
group = "de.wellnerbou"
archivesBaseName = "java-grok"

ext {
    checkStyleToolVersion = "6.19"
    jacocoToolVersion = "0.7.9"

    commonsLang3Version = "3.7"

    junitVersion = "4.12"
    assertjVersion = "3.9.1"
    guavaVersion = "24.0-jre"
}

dependencies {
    compile "org.apache.commons:commons-lang3:$commonsLang3Version"

    testCompile "junit:junit:$junitVersion"
    testCompile "org.assertj:assertj-core:$assertjVersion"
    testCompile "com.google.guava:guava:$guavaVersion"
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

// Needed to avoid including all the jar signing and sonatype setup.
if (project.hasProperty("withDeployment")) {
    apply from: "$rootProject.projectDir/gradle/sonatype.gradle"
}

checkstyle {
    configFile = file("${project.projectDir}/extra/checkstyle/checkstyle.xml")
    toolVersion = "$checkStyleToolVersion"
}

checkstyleMain {
    source = 'src/main/java'
    doLast {
        project.ext.checkType = "main"
    }
}

jacoco {
    toolVersion = "$jacocoToolVersion"
}

jacocoTestReport {
    reports {
        xml.enabled false
        csv.enabled false
    }
}


bintray {
    user = "$publishUser"
    key = "$publishKey"
    publications = ['mavenJava']

    dryRun = false
    publish = true

    pkg {
        userOrg = ""
        repo = 'maven'
        name = 'java-grok'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/paulwellnerbou/java-grok'
    }
}

// user and key configuration for bintray deployment:
// ./gradlew bintrayUpload -PpublishUser=<USER> -PpublishKey=<KEY>
if (!project.hasProperty("publishUser")) {
    ext.publishUser = ""
}
if (!project.hasProperty("publishKey")) {
    ext.publishKey = ""
}
if (!project.hasProperty("publishPassword")) {
    ext.publishPassword = ""
}

afterReleaseBuild.dependsOn bintrayUpload